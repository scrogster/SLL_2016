model{
	#process.model (matrix form)###################################################################
	for(i in 1:tot.sites){
	#------initial occupancy model---------##########
		logit(psi[i])<- B[1]+
		                B[2]*(grassland[i]-0.076)/0.146 +
		                B[3]*(clay[i]-32.68)/16.77+         #roughly centered/scaled soil clay.
		                B[4]*roadside[i] +                  #is the site on a roadside?
		                occ_rf[cluster_id[i]]               #random cluster effect.
		Z[i, 1]~dbern(psi[i])
	for(t in 2:max.time){
  #------persistence model---------------##########
		logit(PHI[i,t])<- C[1]+ 
			              C[2]*(grassland[i]-0.076)/0.146 + #roughly centered/scaled grassland area
			              C[3]*firecode[i] +                 #some fire
			              C[4]*pow(firecode[i],2) +                 #more than 6 fires
		                C[5]*grazing[i] +           #some grazing
		                C[6]*pow(grazing[i], 2) +               #intense grazing
			              persist_rf[cluster_id[i]]         #random cluster effect.
	#------colonisation model--------------##########
		logit(GAMMA[i,t])<-D[1] +
		                D[2]*(grassland[i]-0.076)/0.146 + #roughly centered/scaled grassland area
		                colon_rf[cluster_id[i]]           #random cluster effect.
	#------process model-------------------###########	                
			muZ[i, t]<- Z[i, t-1]*PHI[i, t]   + (1-Z[i, t-1])*GAMMA[i, t]
			Z[i, t]~dbern(muZ[i, t])
			ext[i, t]<-equals(Z[i,t]-Z[i,t-1], -1)  #extinction event indicator
      col[i, t]<-equals(Z[i,t-1]-Z[i,t], -1) #colonisation event indicator
		}
			numexi[i]<-sum(ext[i,2:max.time])  #number of extinction events at each site
		  numcoli[i]<-sum(col[i,2:max.time]) #number of colonisation events at each site
	}
	
	#observation model (vector form)###############################################################
	for(a in 1:tot.surveys){
	#--------obs model for actual lizards--########################################################
		fourier.liz[a]<- BETA[2]*(cos(2*pi*year.frac[a]))+BETA[3]*(sin(2*pi*year.frac[a])) + 
			               BETA[4]*(cos(4*pi*year.frac[a]))+BETA[5]*(sin(4*pi*year.frac[a]))
		logit(p.liz.cond[a])<-BETA[1] +fourier.liz[a]  + 
			BETA[6]*time.of.day[a]+
			BETA[7]*pow(time.of.day[a], 2)+  
			BETA[8]*((TempS[a]-22)/5) +
			BETA[9]*pow((TempS[a]-22)/5, 2) +
			BETA[10]*Tdiff[a]    #effect of air-soil temp diff. 
		p.liz[a]<-p.liz.cond[a]* Z[site[a], t[a]] 
		detect.liz[a]~dbern(p.liz[a])	
	#--------obs model for skin------------########################################################	
		fourier.skin[a]<- BETA[12]*(cos(2*pi*year.frac[a]))+BETA[13]*(sin(2*pi*year.frac[a])) + 
			                BETA[14]*(cos(4*pi*year.frac[a]))+BETA[15]*(sin(4*pi*year.frac[a]))
		logit(p.skin.cond[a])<-BETA[11]  +fourier.skin[a]  
		p.skin[a]<-p.skin.cond[a]* Z[site[a], t[a]] 
		detect.skin[a]~dbern(p.skin[a])	
	}
	
	#priors on regression parameters ###############################################################
  for(b in 1:5) {    B[b]~dt(0, pow(2.5,-2), 1) }   #
  for(c in 1:6) {    C[c]~dt(0, pow(2.5,-2), 1) }  
  for(d in 1:2) {    D[d]~dt(0, pow(2.5,-2), 1) }  
  for(e in 1:15) {BETA[e]~dt(0, pow(2.5,-2), 1)}   
	
	########constant; (pi)##########################################################################
	pi<-3.141592654
  ########keeping track of occ and turnover#######################################################
  Numocc[1]<-sum(Z[,1])
    for(TT in 2:max.time) {Numocc[TT]<-sum(Z[,TT]) 
                              Ext_t[TT-1]<-sum(ext[,TT])             
                             Col_t[TT-1]<-sum(col[,TT])
                          Deficit[TT-1]<-Col_t[TT-1]-Ext_t[TT-1] }
  ########cluster-level random effects############################################################
       clus_var_occ~dt(0, pow(25,-2), 1)T(0, )                     
            tau.occ<-pow(clus_var_occ, -2)
   clus_var_persist~dt(0, pow(25,-2), 1)T(0, )      
            tau.persist<-pow(clus_var_persist, -2)
     clus_var_colon~dt(0, pow(25,-2), 1)T(0, )    
            tau.colon<-pow(clus_var_colon, -2)
  for(kk in 1:num_clust){  
          occ_rf[kk]~dnorm(0, tau.occ)
      persist_rf[kk]~dnorm(0, tau.persist)
        colon_rf[kk]~dnorm(0, tau.colon)
      }
}