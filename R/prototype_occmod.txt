model{
	#process.model (matrix form)###################################################################
	for(i in 1:tot.sites){
	#------initial occupancy model---------##########
		logit(psi[i])<- B[1]+ 
			              B[2]*(grassland[i]-0.076)/0.146 + #roughly centered/scaled grassland area
			              B[3]*firecode[i] +                 #number of fires
		                B[4]*grazing[i] +                  #grazing score
		                B[5]*firecode[i]*grazing[i] +       #fire-grazing interaction.
		                B[6]*(clay[i]-32.68)/16.77         #roughly centered/scaled soil clay.
		Z[i, 1]~dbern(psi[i])
	for(t in 2:max.time){
  #------persistence model---------------##########
		logit(PHI[i,t])<- C[1]+ 
			                C[2]*(grassland[i]-0.076)/0.146 + #roughly centered/scaled grassland area
			                C[3]*firecode[i] +                 #number of fires
		                  C[4]*grazing[i] +                  #grazing score
		                  C[5]*firecode[i]*grazing[i] +       #fire-grazing interaction.
		                  C[6]*(clay[i]-32.68)/16.77         #roughly centered/scaled soil clay.
		                
	#------colonisation model--------------##########
		logit(GAMMA[i,t])<-D[1] +
		                D[2]*(grassland[i]-0.076)/0.146  #roughly centered/scaled grassland area

	#------process model-------------------###########	                
			muZ[i, t]<- Z[i, t-1]*PHI[i, t]   + (1-Z[i, t-1])*GAMMA[i, t]
			Z[i, t]~dbern(muZ[i, t])
			ext[i, t]<-equals(Z[i,t]-Z[i,t-1], -1)  #extinction event indicator
      col[i, t]<-equals(Z[i,t-1]-Z[i,t], -1) #colonisation event indicator
		}
			numexi[i]<-sum(ext[i,2:max.time])  #number of extinction events at each site
		  numcoli[i]<-sum(col[i,2:max.time]) #number of colonisation events at each site
	}
	
	#observation model (vector form)###############################################################
	for(a in 1:tot.surveys){
	#--------obs model for actual lizards--########################################################
		fourier.liz[a]<- BETA[2]*(cos(2*pi*year.frac[a]))+BETA[3]*(sin(2*pi*year.frac[a])) + 
			               BETA[4]*(cos(4*pi*year.frac[a]))+BETA[5]*(sin(4*pi*year.frac[a]))
		logit(p.liz.cond[a])<-BETA[1] +fourier.liz[a]  + 
			BETA[6]*time.of.day[a]+
			BETA[7]*pow(time.of.day[a], 2)+  
			BETA[8]*((TempA[a]-22)/5) +
			BETA[9]*pow((TempA[a]-22)/5, 2) +
			BETA[10]*Tdiff[a] +   #effect of air-soil temp diff. 
			BETA[11]*((TempA[a]-22)/5)*Tdiff[a]  #interaction between air temp, and temp differential
		p.liz[a]<-p.liz.cond[a]* Z[site[a], t[a]] 
		detect.liz[a]~dbern(p.liz[a])	
	#--------obs model for skin------------########################################################	
		fourier.skin[a]<- BETA[13]*(cos(2*pi*year.frac[a]))+BETA[14]*(sin(2*pi*year.frac[a])) + 
			                BETA[15]*(cos(4*pi*year.frac[a]))+BETA[16]*(sin(4*pi*year.frac[a]))
		logit(p.skin.cond[a])<-BETA[12]  +fourier.skin[a]    #bedding in effect.  
		p.skin[a]<-p.skin.cond[a]* Z[site[a], t[a]] 
		detect.skin[a]~dbern(p.skin[a])	
	#-------PP check quantities..---------########################################################		
		fake.liz[a]~dbern(p.liz[a])
		fake.skin[a]~dbern(p.skin[a])
	}
	
	#priors on regression parameters ###############################################################
  for(b in 1:6) {    B[b]~dt(0, pow(2.5,-2), 1) }   #
  for(c in 1:6) {    C[c]~dt(0, pow(2.5,-2), 1) }  
  for(d in 1:2) {    D[d]~dt(0, pow(2.5,-2), 1) }  
  for(e in 1:16) {BETA[e]~dt(0, pow(2.5,-2), 1)}   
	
	########constant; (pi)##########################################################################
	pi<-3.141592654
  ########keeping track of occ and turnover#######################################################
  Numocc[1]<-sum(Z[,1])
    for(TT in 2:max.time) {Numocc[TT]<-sum(Z[,TT]) 
                              Ext_t[TT-1]<-sum(ext[,TT])             
                             Col_t[TT-1]<-sum(col[,TT])
                          Deficit[TT-1]<-Col_t[TT-1]-Ext_t[TT-1] }
}